(define (assoc key records)
    (cond ((null? records) false)
          ((equal? key (caar records)) (car records))
          (else (assoc key (cdr records)))))

; generalized table from ex_3.25
(define (make-table)
    (let ((local-table (list '*table*)))
        (define (assoc keys records)
            (cond ((null? records) #f)
                  ((not (pair? records)) #f)
                  ((not (pair? (car records))) #f)
                  ((equal? (car keys) (caar records)) 
                    (if (null? (cdr keys)) 
                        (car records) 
                        (assoc (cdr keys) (cdar records))))
                  (else (assoc keys (cdr records)))))
        (define (lookup keys)
            (let ((record (assoc keys (cdr local-table))))
                (if record
                    (cdr record)
                    #f)))
        (define (insert! keys value)
            (define (insert-or-update! remaining-keys table)
                (let ((subtable (assoc (list (car remaining-keys)) (cdr table))))
                    (if subtable
                        (if (null? (cdr remaining-keys))
                            (set-cdr! subtable value)
                            (insert-or-update! (cdr remaining-keys) subtable))
                        (begin
                            (let ((new-subtable (list (car remaining-keys))))
                                (set-cdr! table (cons new-subtable (cdr table)))
                                (insert-or-update! remaining-keys table))))))
            (insert-or-update! keys local-table)
            'ok)
        (define (dispatch m)
            (cond ((eq? m 'lookup) lookup)
                  ((eq? m 'insert!) insert!)
                  (else (error "Unknown table operation " m))))
        dispatch))